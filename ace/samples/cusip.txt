//http://rosettacode.org/wiki/CUSIP#Dyalect
func isCusip(s) {
    return false when s.Length() != 9
    var sum = 0
    for i in 0..7 {
        let c = s[i]
        var v =
            match c {
                '0'..'9' => c.Order() - 48,
                'A'..'Z' => c.Order() - 64,
                '*' => 36,
                '@' => 37,
                '#' => 38,
                _ => false
            }
        v *= 2 when i % 2 == 1
        sum += v / 10 + v % 10
    }
    s[8].Order() - 48 == (10 - (sum % 10)) % 10
}
 
let candidates = [
    "037833100",
    "17275R102",
    "38259P508",
    "594918104",
    "68389X106",
    "68389X105"
]
 
for candidate in candidates {
    var b = isCusip(candidate) ? "correct" : "incorrect"
    print("\(candidate) -> \(b)")
}