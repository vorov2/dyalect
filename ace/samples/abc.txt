//http://rosettacode.org/wiki/ABC_problem#Dyalect
func blockable(str) {
    let blocks = [
        "BO", "XK", "DQ", "CP", "NA", "GT", "RE", "TG", "QD", "FS",
        "JW", "HU", "VI", "AN", "OB", "ER", "FS", "LY", "PC", "ZM" ]
    let strUp = str.Upper()
    var final = ""
 
    for c in strUp {
        for j in blocks.Indices() when blocks[j].StartsWith(c) || blocks[j].EndsWith(c) {
            final += c
            blocks[j] = ""
            break
        }
    }
    
    final == strUp
}
 
func canOrNot(can) => can ? "can" : "cannot"
 
for str in [ "A", "BARK", "BooK", "TrEaT", "comMON", "sQuAd", "Confuse" ] {
    print("\"\(str)\" \(canOrNot(blockable(str))) be spelled with blocks.")
}